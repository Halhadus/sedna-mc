buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'net.minecraftforge.gradle'

def getGitRef() {
    try {
        final def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (final Throwable ignored) {
        return "unknown"
    }
}

String semver = "${version_major}.${version_minor}.${version_patch}"

String build_number = System.getenv("PROMOTED_NUMBER");
if (build_number == null)
    build_number = System.getenv("BUILD_NUMBER")
if (build_number == null)
    build_number = getGitRef()

version = "${semver}+${build_number}"
group = mod_group
archivesBaseName = "${mod_name.replace(' ', '-')}-MC${minecraft_version}-Forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    embed
    implementation.extendsFrom embed
}

repositories {
    mavenCentral()
    maven { url "http://maven.cil.li/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    embed('li.cil.ceres:ceres:0.0.2+20') { transitive = false }
    embed('li.cil.sedna:sedna:1.0.0+100') { transitive = false }
    embed('li.cil.sedna:sedna-buildroot:0.0.1+15') { transitive = false }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand([
                'mod_version'          : version,
                'mod_name'             : mod_name,
                'mod_url'              : mod_url,
                'mod_issues_url'       : mod_issues_url,
                'forge_version_min'    : forge_version_min,
                'minecraft_version_min': minecraft_version_min
        ])
    }
}

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                sedna_mc {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                sedna_mc {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            args '--mod', 'sedna_mc', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                sedna_mc {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
    finalizedBy 'reobfJar'

    configurations.embed.each {
        from(project.zipTree(it)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }

    manifest {
        attributes([
                "Specification-Title"     : "sedna_mc",
                "Specification-Vendor"    : "Sangar",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${semver}",
                "Implementation-Vendor"   : "Sangar",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version "MC${minecraft_version}-Forge-${version}"
            artifact jar
        }
    }
    repositories {
        maven {
            url System.getenv("MAVEN_PATH")
        }
    }
}

idea {
    module {
        for (final String exclude in ['out', 'logs', 'run', 'src/generated']) {
            excludeDirs += file(exclude)
        }
    }
}
